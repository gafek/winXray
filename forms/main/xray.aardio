import fonts.fontAwesome;
import win.ui;
import win.ui.atom;
/*DSG{{*/
var frmXray = win.form(text="winXray ";right=959;bottom=591;bgcolor=16777215)
frmXray.add(
btnEditPac={cls="plus";text="编辑PAC";left=332;top=366;right=438;bottom=396;align="left";db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=12}};iconText='\uF1C4';notify=1;textPadding={left=30};z=7};
btnImportServerFromClipBd={cls="plus";text="批量导入链接";left=459;top=366;right=592;bottom=396;align="left";db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=12}};iconText='\uF196 ';notify=1;textPadding={left=30};z=8};
btnTcping={cls="plus";text="检测并连接最快服务器";left=735;top=366;right=941;bottom=398;align="left";bgcolor=11580047;border={radius=4};db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=18}};iconText='\uF012';notify=1;textPadding={left=42};z=3};
chkAutoTest={cls="plus";text="异常自动重连 ";left=598;top=366;right=729;bottom=396;align="left";db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=12}};iconText='\uF0C8';notify=1;textPadding={left=30};z=9};
edit={cls="edit";left=6;top=405;right=953;bottom=585;bgcolor=0;color=16777215;db=1;dl=1;dr=1;edge=1;hscroll=1;multiline=1;vscroll=1;z=2};
listview={cls="listview";left=4;top=5;right=954;bottom=358;db=1;dl=1;dr=1;dt=1;edge=1;fullRow=1;gridLines=1;z=1};
radioProxy={cls="radiobutton";text="全局代理";left=129;top=369;right=226;bottom=395;bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-13);z=4};
radioProxyDirect={cls="radiobutton";text="不使用代理";left=14;top=369;right=120;bottom=395;bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-13);z=6};
radioProxyPac={cls="radiobutton";text="PAC代理";left=234;top=369;right=321;bottom=395;bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-13);z=5}
)
/*}}*/

frmXray.listview.insertColumn("类型",100) 
frmXray.listview.insertColumn("服务器",160) 
frmXray.listview.insertColumn("端口",70) 
frmXray.listview.insertColumn("加密",90) 
frmXray.listview.insertColumn("协议",50) 
frmXray.listview.insertColumn("备注",190) 
frmXray.listview.insertColumn("响应速度",80) 
frmXray.listview.insertColumn("状态",-1) 
frmXray.show();

import config;
import xray.core; 
var currentOutboundIndex,currentOutboundAddress;
var activeOutbound = function(outboundIndex,outboundAddress,enableRetry){
    if(!outboundIndex && !outboundAddress){
    	outboundIndex,outboundAddress = currentOutboundIndex,currentOutboundAddress;
    	if(!outboundIndex && !outboundAddress) return;
    }
    
    var outbound = config.proxy.outbounds[outboundIndex];
    if(!outbound){
    	return ; 
    }
    
	var currentTesting = currentOutboundIndex == outboundIndex && currentOutboundAddress == outboundAddress;
	if(config.core.default.inbounds[1]["port"] && config.core.default.inbounds[1]["port"]!=xray.core.getPort()){
	 	currentTesting = false;
	}
	
	if( currentTesting || xray.core.restart(frmXray.edit,outbound) ){
		currentOutboundIndex,currentOutboundAddress = outboundIndex,outboundAddress;
		frmXray.autoTesting = true;
		
		..thread.invoke( 
			function(frmXray,outboundIndex,outboundAddress,port,enableRetry,currentTesting){
				import wsock.tcp.socks5Client;
			 
				for(i=1;2;1){ 
					var client;
					for(i=1;5;1){
						sleep(1000);
						client = wsock.tcp.socks5Client("127.0.0.1",port); 
						if(client)break;
					}
					
					if(!client){
						continue;
					};
					
					client.setTimeouts(1000,1000); 
					if(client.connect("www.google.com",80) ){
						var sendData =/***********
GET /  HTTP/1.1
Host: www.google.com
Connection: close
User-Agent: Mozilla/5.0 (Windows NT 6.1; WOW64)
Accept: */*;  
Accept-Language: zh-CN,zh; 
Accept-Charset:utf-8;  
***********/
						client.write( sendData + '\r\n\r\n' )
						var rep = client.read(9);
						client.close();
						
						if(outboundAddress!=frmXray.listview.getItemText(outboundIndex,2)){
							return;
						}
					
						if( rep && ..string.startWith(rep,"HTTP/1.1 ") ){
							
							import web.rest.client;
							var restClient = web.rest.client(,"SOCKS=127.0.0.1:"+port);
							restClient._http.setTimeouts(1000,2000,2000);
							restClient.get("https://www.google.com/generate_204");
							var lastStatusCode = restClient.lastStatusCode;
							restClient.close();
 	
							if( lastStatusCode == 204 ){
								frmXray.onStartXrayComplete(outboundIndex,currentTesting);
								return;	
							}
							
						} 
					}
				}
				frmXray.onStartXrayFailed(outboundIndex,enableRetry);
			},frmXray,outboundIndex,outboundAddress,xray.core.getPort(),enableRetry,currentTesting
		)
	}	
}

frmXray.onStartXrayComplete = function(outboundIndex,currentTesting){
	frmXray.listview.ensureVisible(outboundIndex);
	
	for b,v in table.eachIndex(config.proxy.outbounds){
		frmXray.listview.setItemText("",b,8);
	}
	
	frmXray.autoTesting = false;
	frmXray.listview.setItemText("活动服务器：正常",outboundIndex,8);

	if(!currentTesting) frmXray.edit.print("已切换到服务器：",frmXray.listview.getItemText(outboundIndex,2));

	if(frmXray.listview.getItemText(outboundIndex,7)=="不可用"){
		frmXray.listview.setItemText("...",outboundIndex,7);
	}
		
	frmXray.onTcpingReturn = function(){};
	frmXray.btnTcping.disabledText = null;
	frmXray.validOutbounds = {};
}

frmXray.onStartXrayFailed = function(outboundIndex,enableRetry){
	frmXray.listview.ensureVisible(outboundIndex);
	
	
	if(enableRetry){
		if(#frmXray.validOutbounds){
			var next = table.shift(frmXray.validOutbounds);
			activeOutbound(next[1],next[2],#frmXray.validOutbounds);
			frmXray.listview.setItemText("异常,测试下一个...",outboundIndex,8);
			frmXray.edit.print("服务器异常：",frmXray.listview.getItemText(outboundIndex,2)); 
			frmXray.edit.print("正在尝试连接：",next[2]); 	
		}
		else {
			frmXray.btnTcping.oncommand();
		}
	}
	else {
		frmXray.listview.setItemText("活动服务器：异常",outboundIndex,8);
		frmXray.edit.print("已切换到服务器：",frmXray.listview.getItemText(outboundIndex,2)); 
		frmXray.autoTesting = false;
	} 
	
	frmXray.btnTcping.disabledText = null;
}

var tcping = function(frmXray,outboundIndex,address){ 
	import wsock.tcp.client; 
	var item = frmXray.listview.items[outboundIndex];
	
	var timeout = 0; 
	for(i=1;8;1){ 
		var client = wsock.tcp.client(); 	
		var tickBegin = time.tick(); 
		if(client.connectTimeout(item[2],tonumber(item[3]),3)){
			var ret = client.send("."); 
			client.close();
			
			if( ret != 1){ 
				frmXray.listview.setItemText("不可用",outboundIndex,7);
				frmXray.onTcpingReturn(outboundIndex,address,false);
				return;
			}
			timeout =  timeout + (time.tick() - tickBegin);
		}
		else {
			client.close();
			frmXray.listview.setItemText("不可用",outboundIndex,7);
			frmXray.onTcpingReturn(outboundIndex,address,false);
			return;
		} 
	}
	
	frmXray.onTcpingReturn(outboundIndex,address,true);
	frmXray.listview.setItemText(math.round(timeout / 8 / 1000,2)+"秒",outboundIndex,7);
};

import style;
frmXray.btnTcping.skin(style.primaryButton);
frmXray.btnTcping.oncommand = function(id,event){
	
	var items = frmXray.listview.items;
	if(#items){
		frmXray.btnTcping.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
		if(!xray.core.getPath()){
			frmXray.btnTcping.disabledText = null;
			frmXray.msgErr("下载 xray.exe 失败")
			return;
		}
		
		var faieldCount = 0;
		frmXray.onTcpingReturn = function(outboundIndex,address,succeeded){
			if(address!=frmXray.listview.getItemText(outboundIndex,2)){
				return; 
			}
			
			if(config.proxy.outbounds[outboundIndex].autoConnect === false ){
				faieldCount++;
				if(faieldCount==#items) frmXray.autoTesting = false;
				return;
			}
			
			if(succeeded){
				frmXray.validOutbounds = {}
				frmXray.onTcpingReturn = function(outboundIndex,address,succeeded){
					if(address!=frmXray.listview.getItemText(outboundIndex,2)){
						return; 
					}
					
					if(config.proxy.outbounds[outboundIndex].autoConnect === false ){
						faieldCount++;
						if(faieldCount==#items) frmXray.autoTesting = false;
						return;
					}
					
					if(succeeded) table.push(frmXray.validOutbounds,{outboundIndex;address});
					else{
						faieldCount++;
						if(faieldCount==#items) frmXray.autoTesting = false;
					}
				}
				activeOutbound(outboundIndex,address,#items>1);	
			}
			else{
				faieldCount++;
				if(faieldCount==#items) frmXray.autoTesting = false;
			}
		}
		
		for b,v in table.eachIndex(config.proxy.outbounds){
			frmXray.listview.setItemText("...",b,7);
			frmXray.listview.setItemText("",b,8);
		}
		
		var handles = {}	
		thread.createSuspended(true);
		for(i=1;#items;1){
			var h = thread.create(tcping,frmXray,i,frmXray.listview.getItemText(i,2));	 
			table.push(handles,h);
		} 
		thread.createSuspended(false);
		
		var dnsapi = ..raw.loadDll("dnsapi.dll");
		dnsapi.DnsFlushResolverCacheB();
		
		for(i=1;#handles;1){
			var h = handles[i];	 
			thread.resume(h);
			raw.closehandle(h);
		} 
		
		..thread.waitOne(handles);
		frmXray.btnTcping.disabledText = null;
	}
	else {
		if(id) frmXray.msgWarn("请先添加服务器")
	}
}

frmXray.onDestroy = function(){
	xray.core.stop();
}

frmXray.listview.onnotify = function(id,code,ptr){
	select(code) {
		case 0xFFFFFFF4/*_NM_CUSTOMDRAW*/ {
			var lvcd = frmXray.listview.getNotifyCustomDraw(code,ptr);
			if( lvcd.nmcd.dwDrawStage == 0x10001/*_CDDS_ITEMPREPAINT*/)
				return 0x20/*_CDRF_NOTIFYSUBITEMDRAW*/
			elseif( lvcd.nmcd.dwDrawStage == 1/*_CDDS_PREPAINT*/ ){
				return 0x20/*_CDRF_NOTIFYITEMDRAW*/;
			}
			elseif( lvcd.nmcd.dwDrawStage == ( 0x10001/*_CDDS_ITEMPREPAINT*/ | 0x20000/*_CDDS_SUBITEM*/) ){ 
				//注意这里 iSubItem 的索引自0开始( 其他函数通常自1开始 )
				lvcd.clrText = lvcd.nmcd.dwItemSpec % 2 ? 0x373737 : 0;
				lvcd.clrTextBk = lvcd.nmcd.dwItemSpec % 2 ? 0xFFFFFF : 0xF5F5F5;
				lvcd.update()
				
				return 0/*_CDRF_DODEFAULT*/
			}
		}
		case  0xFFFFFFFD/*_NM_DBLCLK*/ {
			var nm = frmXray.listview.getNotifyMessage(code,ptr);
			if( nm ){
				frmXray.onTcpingReturn = function(i){}
				frmXray.btnTcping.disabledText = null;
				
				for b,v in table.eachIndex(config.proxy.outbounds){
					frmXray.listview.setItemText("",b,8);
				}
				activeOutbound(nm.iItem,frmXray.listview.getItemText(nm.iItem,2),false); 
			}
		}
		case 0xFFFFFFFB/*_NM_RCLICK*/  {
			var x,y = win.getCursorPos();
			var nm = frmXray.listview.getNotifyMessage(code,ptr);
			if(!nm) return;
			
			var currentIdx = nm.iItem;
			if(!currentIdx){
				return;
			}
			
			var selectedItems = frmXray.listview.selected; 
			var popmenu = win.ui.popmenu(frmXray);
			
			if(#selectedItems==1){
				popmenu.add('设为活动服务器( 鼠标双击 )',function(id){
					if(currentIdx){
						frmXray.onTcpingReturn = function(i){}
						frmXray.btnTcping.disabledText = null;
						
						for b,v in table.eachIndex(config.proxy.outbounds){
							frmXray.listview.setItemText("",b,8);
						}
						activeOutbound(currentIdx,frmXray.listview.getItemText(currentIdx,2),false)
					}
				});
				
				popmenu.add('允许被自动选择为活动服务器',function(id){
					if(currentIdx){
						if(config.proxy.outbounds[currentIdx].autoConnect === false){
							config.proxy.outbounds[currentIdx].autoConnect = null;
						}
						else {
							config.proxy.outbounds[currentIdx].autoConnect = false;
						}
						
					}
				});
				popmenu.check(2,config.proxy.outbounds[currentIdx].autoConnect!==false);
				
				popmenu.add();
				popmenu.add('SSH 登录服务器',function(id){
					if(currentIdx){
						if(_STUDIO_INVOKED){
    						frmXray.msgErr("请在发布后运行此功能");
    						return;
    					}
						import process;
						process.execute(io._exepath,"/sshLogin root@" + config.proxy.outbounds[currentIdx].address);
					}
				});
				
				popmenu.add();

				popmenu.add('复制服务器分享链接',function(id){
					if(currentIdx){
						import xray.config;
						import win.clip;
						
						var str = xray.config.exportToString({config.proxy.outbounds[currentIdx]});
						if(str){ 
							win.clip.write(str); 
							frmXray.edit.print("已复制链接:",str);
						}  
					}
				});
				popmenu.add('复制服务器地址',function(id){
					if(currentIdx){
						import win.clip;
						win.clip.write(config.proxy.outbounds[currentIdx].address + ":" + config.proxy.outbounds[currentIdx].port); 
						frmXray.edit.print("已复制服务器地址:",config.proxy.outbounds[currentIdx].address + ":" + config.proxy.outbounds[currentIdx].port);
					}
				});
				popmenu.add('复制服务器 JSON 配置',function(id){
					if(currentIdx){
						import win.clip;
						import web.json; 
						var str = web.json.stringify(config.proxy.outbounds[currentIdx],true,false);
						win.clip.write(str);
						frmXray.edit.print(str);
					}
				});
				popmenu.add();
				
				popmenu.add('全球范围 Ping 检测',function(id){
					if(currentIdx){
						process.openUrl("http://ping.pe/" + config.proxy.outbounds[currentIdx].address)
					}
				});
				
				popmenu.add('全球范围端口检测',function(id){
					if(currentIdx){
						process.openUrl("http://port.ping.pe/" + config.proxy.outbounds[currentIdx].address + ":" + config.proxy.outbounds[currentIdx].port)
					}
				});
				
				popmenu.add();
				
				if(config.proxy.outbounds[currentIdx].port == 443 && config.proxy.outbounds[currentIdx].tls){
					popmenu.add('浏览 HTTP',function(id){
						if(currentIdx){
							process.openUrl("http" ++ "://" + config.proxy.outbounds[currentIdx].address); 
						}
					});	
					popmenu.add('浏览器 HTTPS',function(id){
						if(currentIdx){
							process.openUrl("https" ++ "://" + config.proxy.outbounds[currentIdx].address); 
						}
					});	
					popmenu.add();
				}
			}
			
			popmenu.add('删除服务器',function(id){
				if(currentIdx){
					for(i=#selectedItems;1;-1){
  						var item = selectedItems[i] 
						frmXray.listview.delItem(item);
						..table.remove(config.proxy.outbounds,item);
					}
					 
					publish("outbounds.updateConfigJson");
				}
			});
			popmenu.popup(x,y,true);
		}
	}
}

import xray.core;
subscribe("xray.core.reload",function(){ 
	config.proxy.outbounds.fields = {"protocol";"address";"port";"security";"network";"ps";}
	frmXray.listview.setTable(config.proxy.outbounds);	 
	
	frmXray.btnTcping.oncommand();
} )

import process;
if(#config.proxy.outbounds){
	frmXray.setTimeout( 
		function(){
			publish("xray.core.reload");	
		},1000
	);	
}

subscribe("config.proxy.mode.changed",function(...){
	frmXray.radioProxyPac.checked = false;
	frmXray.radioProxy.checked = false;
	frmXray.radioProxyDirect.checked = false;
	
	if(config.proxy.mode = "pac"){
		frmXray.radioProxyPac.checked = true;
	}
	elseif(config.proxy.mode = "proxy"){
		frmXray.radioProxy.checked = true;
	}
	else{
		frmXray.radioProxyDirect.checked = true;
	}	
	
	xray.core.updateGitConfigGithub();
} )
publish("config.proxy.mode.changed");

var updatProxySetting = function(){
	if(frmXray.radioProxyPac.checked){
		config.proxy.mode = "pac";
		inet.conn.setProxyAutoConfig(,xray.core.getPacUrl() )		
	}
	elseif(frmXray.radioProxy.checked){
		config.proxy.mode = "proxy";
		inet.conn.setProxy(,"SOCKS=127.0.0.1:"+xray.core.getPort());	
	}
	else {
		config.proxy.mode = "direct";
		inet.conn.setProxy();	
	}
	
	xray.core.updateGitConfigGithub();
	config.proxy.save();
}
frmXray.radioProxyPac.oncommand = updatProxySetting;
frmXray.radioProxy.oncommand = updatProxySetting;
frmXray.radioProxyDirect.oncommand = updatProxySetting;

import win.dlg.message
win.dlg.message.install()
frmXray.btnEditPac.skin(style.plainButton)
frmXray.btnEditPac.oncommand = function(id,event){
	if(frmPac && win.isWindow(frmPac.hwnd)){
		if( isIconic(frmPac.hwnd) ) win.show(frmPac.hwnd,9/*_SW_RESTORE*/ );
		if( !isVisible(frmPac.hwnd) ) win.show(frmPac.hwnd,0x1/*_SW_NORMAL*/ );
		win.setForeground(frmPac.hwnd)
		return;
	}

	frmPac = frmXray.loadForm("\forms\main\pac.aardio");
	frmPac.show();
}
subscribe("showPacForm",function(...){
	frmXray.btnEditPac.oncommand();
} )

frmXray.btnImportServerFromClipBd.skin(style.plainButton)
frmXray.btnImportServerFromClipBd.oncommand = function(id,event){
	var outbounds = xray.config.importFromClip();
	if(#outbounds){
		..table.append(config.proxy.outbounds,outbounds); 
		publish("xray.core.reload");
		publish("outbounds.updateConfigJson");
		frmXray.msgOk("已成功导入" + #outbounds + "个服务器",1200); 
		return; 
	}

	frmXray.msgFrown('未导入服务器！ \r\n请先复制以下格式服务器分享链接：\r\n\r\nvmess:// 链接\r\nvless:// 链接\r\nss:// 链接\r\ntrojan:// 链接\r\ntrojan-go:// 链接\r\nhttp:// 或 https:// 订阅源！')
}

var testTimerId;
frmXray.chkAutoTest.oncommand = function(id,event){
	if(testTimerId){
		frmXray.clearInterval(testTimerId);
	}

	config.proxy.test = frmXray.chkAutoTest.checked;
	if(!config.proxy.test) return;

	testTimerId = frmXray.setInterval( 
		15000,function(){
			if( frmXray.autoTesting ) return;
			frmXray.autoTesting = true;

			activeOutbound(,,true);
		} 
	)
}

if(config.proxy.test){
	frmXray.chkAutoTest.checked = true;
	frmXray.chkAutoTest.oncommand();
}

frmXray.chkAutoTest.skin(style.checkBox);

frmXray.enableDpiScaling();
return win.loopMessage();