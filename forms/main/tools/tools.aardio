import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio form";right=759;bottom=469;bgcolor=16777215)
winform.add(
btnAardio={cls="plus";text="aardio 桌面软件快速开发（ 开发环境仅 6.5MB ）";left=44;top=403;right=478;bottom=445;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF17A';notify=1;textPadding={left=50};z=3};
btnDnsFlush={cls="plus";text="清空DNS缓存";left=44;top=193;right=430;bottom=235;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF021';notify=1;textPadding={left=50};z=5};
btnGitHub={cls="plus";text="github 网速优化";left=44;top=298;right=280;bottom=340;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF09B';notify=1;textPadding={left=50};z=2};
btnSshInstall={cls="plus";text="一键安装 Xray 服务端";left=349;top=37;right=575;bottom=79;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF17C';notify=1;textPadding={left=50};z=10};
btnUpdateSystemTime={cls="plus";text="同步系统时间（ 修正 Xray 连接报错 ）";left=44;top=141;right=430;bottom=183;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF017';notify=1;textPadding={left=50};z=4};
btnUpdateXrayCore={cls="plus";text="更新 Xray Core";left=44;top=88;right=287;bottom=130;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF01A';notify=1;textPadding={left=50};z=6};
btnWubiLex={cls="plus";text="WIN10 五笔助手（仅830KB，自带五笔86、98、091、新世纪、郑码……）";left=44;top=351;right=679;bottom=393;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-24;name='FontAwesome');padding={left=18}};iconText='\uF11C';notify=1;textPadding={left=50};z=7};
btnYoutube={cls="plus";text="youtube 视频下载工具";left=44;top=246;right=280;bottom=288;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF167';notify=1;textPadding={left=50};z=1};
editSshHost={cls="plus";left=176;top=47;right=355;bottom=71;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};dl=1;dt=1;editable="edit";font=LOGFONT(h=-16);textPadding={bottom=1};z=8};
static={cls="static";text="账号@服务器：";left=52;top=51;right=167;bottom=78;align="right";dl=1;dt=1;font=LOGFONT(h=-13);transparent=1;z=9}
)
/*}}*/

import style;
winform.btnYoutube.skin(style.plainButton);

var frmYoutube;
winform.btnYoutube.oncommand = function(id,event){
	if(frmYoutube&&win.isWindow(frmYoutube.hwnd)){
		if(win.isIconic(frmYoutube.hwnd)){
			frmYoutube.show(9/*_SW_RESTORE*/);	
		}
		
		frmYoutube.show();
		return;
	}
	
	frmYoutube = winform.loadForm("\forms\main\tools\youtube.aardio");
	frmYoutube.show();
}
 
import process;
winform.btnGitHub.skin(style.plainButton)
winform.btnGitHub.oncommand = function(id,event){
    if(_STUDIO_INVOKED){
    	winform.msgboxErr("请在发布后运行此功能");
    	return;
    }
    
	process.execute(io._exepath,"/github","runas");
}

winform.btnAardio.skin(style.plainButton)
winform.btnAardio.oncommand = function(id,event){
	process.openUrl("http://www.aardio.com")
}

winform.btnUpdateSystemTime.skin(style.plainButton)
winform.btnUpdateSystemTime.oncommand = function(id,event){
    if(_STUDIO_INVOKED){
    	winform.msgboxErr("请在发布后运行此功能");
    	return;
    }
    
    winform.btnUpdateSystemTime.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	if( process.executeWait(io._exepath,"/updateTime","runas") ){
		winform.btnUpdateSystemTime.disabledText = null;
		winform.btnUpdateSystemTime.text = '时间已同步'
		winform.setTimeout( 
			function(){
				winform.btnUpdateSystemTime.text = '同步系统时间（ 修正 xray 连接报错 ）'
			},2000) 
	}
	else {
		winform.btnUpdateSystemTime.disabledText = null;
	} 
}

winform.btnDnsFlush.skin(style.plainButton)
winform.btnDnsFlush.oncommand = function(id,event){
    winform.btnDnsFlush.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}

    import process.popen;
	var prcs = process.popen(,"ipconfig /flushdns");
	var str = prcs.read(-1)
	
    var dnsapi = ..raw.loadDll("dnsapi.dll");
	dnsapi.DnsFlushResolverCacheB();
		
	winform.msgInfo(str);
    winform.btnDnsFlush.disabledText = null;
}

winform.btnUpdateXrayCore.skin(style.plainButton)
winform.btnUpdateXrayCore.oncommand = function(id,event){
    winform.btnUpdateXrayCore.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'};
    
	import xray.core;
	var versionTag = xray.core.updateCore();
	if( versionTag ){
		publish("xray.core.reload");
		winform.btnUpdateXrayCore.disabledText = null;
		winform.msgOk("Xray Core 已更新到：" + versionTag,1500)
	}
	else {
		winform.btnUpdateXrayCore.disabledText = null;
	} 
}

import process;
winform.btnWubiLex.skin(style.plainButton)
winform.btnWubiLex.oncommand = function(id,event){
	process.openUrl("http://wubi.aardio.com/");
}

winform.editSshHost.setCueBannerText("root@0.0.0.0")
winform.btnSshInstall.skin(style.plainButton)
winform.btnSshInstall.oncommand = function(id,event){
    
    if(_STUDIO_INVOKED){
    	winform.msgErr("请在发布后运行此功能");
    	return;
    }
    
    config.ui.sshServer = winform.editSshHost.text;
    config.ui.save();
    
    winform.btnSshInstall.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
    
    var server = winform.editSshHost.text;
    if( ! ..string.indexOf(server,"@") ){
    	server = "root@" + server;
    }
	process.execute(io._exepath,"/sshInstall " + server);
	win.delay(1000);
	winform.btnSshInstall.disabledText = null;
}

import config;
if(config.ui.sshServer){
	winform.editSshHost.text = config.ui.sshServer;
}

winform.enableDpiScaling();
winform.show();
win.loopMessage();